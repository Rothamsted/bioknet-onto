PREFIX bk: <http://www.ondex.org/bioknet/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

# Takes names coming from ONDEX and adds new, more suitable names, marking the old entities as
# preferred in the ONDEX platform. This allows us to rename ONDEX data coming from old names.
#
# This query is a first part that migrates the bk:isOndexPreferred property only
# Then, other definitions about the old properties are migrated by ondex_defs_migrate_new_uris.sparql 

DELETE {
  ?src bk:isOndexPreferred ?isOndexPreferred.
}
INSERT {
  ?dst ?eqProp ?src.
  ?dst dcterms:identifier ?newId.
  ?dst bk:isOndexPreferred true.
}
WHERE 
{
  VALUES (?src ?dst ?eqProp )
  {
    # Classes
    ( bk:Comp bk:Compound owl:equivalentClass )
    ( bk:Protcmplx bk:ProteinComplex owl:equivalentClass )
    ( bk:MolCmplx bk:MoleculeComplex owl:equivalentClass )
    ( bk:CelComp bk:CellularComponent owl:equivalentClass )
    ( bk:MolFunc bk:MolecularFunction owl:equivalentClass )
    ( bk:BioProc bk:BiologicalProcess owl:equivalentClass )

    # Relations
    ( bk:physical bk:physical_relation owl:equivalentProperty ) # TODO: how is it actually used?
    ( bk:pub_in bk:published_in owl:equivalentProperty )
    ( bk:genetic bk:genetic_relation owl:equivalentProperty )
    ( bk:h_s_s bk:has_similar_sequence owl:equivalentProperty )
    ( bk:enc bk:encodes owl:equivalentProperty )
    ( bk:enc_by bk:encoded_by owl:equivalentProperty )
    ( bk:occ_in bk:occurs_in owl:equivalentProperty )
    ( bk:cooc_wi bk:occurs_with owl:equivalentProperty )
    ( bk:pd_by bk:produced_by owl:equivalentProperty )
    ( bk:rg_by bk:regulated_by owl:equivalentProperty )
    ( bk:re_by bk:repressed_by owl:equivalentProperty )
    ( bk:ac_by bk:activated_by owl:equivalentProperty )
    ( bk:ex_by bk:expressed_by owl:equivalentProperty )
    ( bk:in_by bk:inhibited_by owl:equivalentProperty )
    ( bk:cs_by bk:consumed_by owl:equivalentProperty )
    ( bk:pu_by bk:taken_up_by owl:equivalentProperty )
    ( bk:cat_c bk:catalyzing_class owl:equivalentProperty )
    ( bk:ca_by bk:catalyzed_by owl:equivalentProperty )
    ( bk:equ bk:equivalent owl:equivalentProperty )
  }
  
  BIND ( STRAFTER ( STR ( ?dst ), STR ( bk: ) ) AS ?newId )
  OPTIONAL { ?src bk:isOndexPreferred ?isOndexPreferred }
}

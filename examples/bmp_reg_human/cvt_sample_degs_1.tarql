PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>
PREFIX bkds: <http://knetminer.org/data/rdf/terms/biokno/dataSources/>
PREFIX bkev: <http://knetminer.org/data/rdf/terms/biokno/evidences/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT
{
  ?protIri bk:expressed_by ?sampleIri.

  ?degRelIri
    a bk:Relation;
    bka:PVALUE     ?pValue;
    bk:evidence    bkev:EXP; # Inferred from experiment
    bk:relFrom     ?protIri; # Details defined by UniProt info
    bk:relTo       ?sampleIri; # Details defined by sample_degs_2.tsv
    bk:relTypeRef  bk:expressed_by
  .
}
WHERE {
  # Some IDs and IRIs to be defined above
  BIND ( LCASE ( REPLACE ( ?Sample, ' ', '_' ) ) AS ?sampleId )
  BIND ( IRI ( CONCAT ( STR ( bkr: ), ?Gene_Symbol ) ) AS ?protIri )
  BIND ( IRI ( CONCAT ( STR ( bkr: ), 'degex_', ?sampleId ) ) AS ?sampleIri )
  BIND ( IRI ( CONCAT ( STR ( bkr: ), 'degex_', ?sampleId, '_', LCASE ( ?Gene_Symbol ) ) ) AS ?degRelIri )
  BIND ( xsd:double ( ?p_value ) AS ?pValue )
}

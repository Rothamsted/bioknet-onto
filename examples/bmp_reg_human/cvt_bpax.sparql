PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>
PREFIX bkds: <http://knetminer.org/data/rdf/terms/biokno/dataSources/>
PREFIX bkev: <http://knetminer.org/data/rdf/terms/biokno/evidences/>
PREFIX bp: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

CONSTRUCT {
  ?path a bk:Path;
    bk:prefName ?pathName;
    bk:evidence bkev:IMPD.

  ?bkProt a bk:Protein;
    dc:identifier ?bkProtAccUri;
    bk:prefName ?protName;
    bk:participates_in ?path;
    bk:evidence bkev:IMPD.
    
  ?bkProtAccUri a bk:Accession;
    dcterms:identifier ?protName;
    bk:dataSource bkds:UNIPROTKB.
}
WHERE
{
  ?path a bp:Pathway;
    bp:displayName ?pathName;
    bp:pathwayComponent ?comp.

  {
    ?comp a bp:BiochemicalReaction;
      bp:left|bp:right ?protein.
  }
  UNION {
    ?react a bp:Complex;
      bp:component ?protein.
  }

  ?protein a bp:Protein;
    bp:displayName ?protName.

  BIND ( IRI ( CONCAT ( STR ( bkr: ), STR ( ?protName ) ) ) AS ?bkProt )
  BIND ( IRI ( CONCAT ( STR ( ?bkProt ), "_acc" ) ) AS ?bkProtAccUri  )
}
